isEmpty(QMAKE_MAPSYM):error("'sym' is present in CONFIG but QMAKE_MAPSYM is not defined!")

contains(TEMPLATE, ".*lib"):dll|contains(TEMPLATE, ".*app") { 
    build_pass|isEmpty(BUILDS) {
        QMAKE_POST_LINK = $$QMAKE_POST_LINK$$escape_expand(\\n)
        QMAKE_POST_LINK = $$QMAKE_POST_LINK$$QMAKE_IFNDEF FAST_BUILD$$escape_expand(\\n\\t)
        QMAKE_POST_LINK = $$QMAKE_POST_LINK$$QMAKE_RUN_MAPSYM
        QMAKE_CLEAN += $$QMAKE_SYM_FILE
        !contains(CONFIG, map) {
            QMAKE_LFLAGS += $$QMAKE_LFLAGS_MAP # from map.prf
            QMAKE_POST_LINK = $$QMAKE_POST_LINK$$escape_expand(\\n\\t)$(DEL_FILE) $$QMAKE_MAP_FILE
        }
        # also copy .sym file to DLLDESTDIR (as Win32MakefileGenerator does for .dll)
        !isEmpty(DLLDESTDIR) {
            for(d, DLLDESTDIR) {
                QMAKE_POST_LINK = $$QMAKE_POST_LINK$$escape_expand(\\n\\t)-$(COPY_FILE) $$QMAKE_SYM_FILE $$fixpath_target($$d)
            }
        }
        QMAKE_POST_LINK = $$QMAKE_POST_LINK$$escape_expand(\\n)$$QMAKE_ENDIF
        
        # also install .sym file to target.path
        contains(INSTALLS, target):!contains(CONFIG, no_install_debuginfo) {
            target_debuginfo.path = $$target.path
            target_debuginfo.files = $$QMAKE_SYM_FILE
            target_debuginfo.CONFIG += data no_check_exist no_vpath no_fixify
            INSTALLS += target_debuginfo
        }

        # also install .sym file to dlltarget.path
        contains(INSTALLS, dlltarget):!contains(CONFIG, no_install_debuginfo) {
            dlltarget_debuginfo.path = $$dlltarget.path
            dlltarget_debuginfo.files = $$QMAKE_SYM_FILE
            dlltarget_debuginfo.CONFIG += data no_check_exist no_vpath no_fixify
            INSTALLS += dlltarget_debuginfo
        }
    }
}
